
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//Начало.Ткачев_июнь 2025, homework-11-2 Задача №1.  Добавляем программно новый реквизит на форму  
	
	//Добавляем на форму реквизит "КонтактноеЛицо"
	Если Элементы.Найти("КонтактноеЛицо") = Неопределено Тогда
		ЭлементКонтактноеЛицо 							= Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Элементы.ГруппаШапкаПраво);
		ЭлементКонтактноеЛицо.Вид						= ВидПоляФормы.ПолеВвода;
		ЭлементКонтактноеЛицо.ПутьКДанным 				= "Объект.Д_КонтактноеЛицо";    
	КонецЕсли;
	
	//Конец.Ткачев_июнь 2025, homework-11-2 Задача №1.  Добавляем программно новый реквизит на форму
	
	//Начало.Ткачев_июнь 2025, homework-11-2 Задача №2.  Добавляем программно новый реквизит на форму  
	
	//Добавляем на форму группу формы "ГруппаРасчетСкидки" для размещения в ней реквизита "СогласованнаяСкидка" и кнопки "ПересчитатьТаблицу" 
	Если Элементы.Найти("ГруппаРасчетСкидки") = Неопределено Тогда
		ЭлементГруппаРасчетСкидки 						= Элементы.Добавить("ГруппаРасчетСкидки", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
		ЭлементГруппаРасчетСкидки.Вид 					= ВидГруппыФормы.ОбычнаяГруппа;
		ЭлементГруппаРасчетСкидки.Отображение 			= ОтображениеОбычнойГруппы.Нет;
		ЭлементГруппаРасчетСкидки.ОтображатьЗаголовок 	= Ложь;  
		ЭлементГруппаРасчетСкидки.Группировка 			= ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;      
	КонецЕсли;
	
	//Добавляем на форму реквизит "СогласованнаяСкидка" 
	Если Элементы.Найти("СогласованнаяСкидка") = Неопределено Тогда
		ЭлементСогласованнаяСкидка 						= Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Элементы.ГруппаРасчетСкидки); 
		ЭлементСогласованнаяСкидка.Вид 					= ВидПоляФормы.ПолеВвода; 
		ЭлементСогласованнаяСкидка.ПутьКДанным 			= "Объект.Д_СогласованнаяСкидка"; 
		ЭлементСогласованнаяСкидка.МаксимальноеЗначение	= 100;
		ЭлементСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "Д_СогласованнаяСкидкаПриИзменении");
	КонецЕсли;
	
	//Создаём команду формы "ПересчитатьТаблицу"
	КомандаПересчитатьТаблицу 							= Команды.Добавить("ПересчитатьТаблицу");
	КомандаПересчитатьТаблицу.Заголовок 				= "Пересчитать таблицу";  
	КомандаПересчитатьТаблицу.Действие 					= "Д_ПересчитатьТаблицу";   
	
	//Добавляем на фопрму кнопку "ПересчитатьТаблицу"
	Если Элементы.Найти("ПересчитатьТаблицу") = Неопределено Тогда
		ЭлементКнопкаПересчитатьТаблицу 				= Элементы.Добавить("ПересчитатьТаблицу", Тип("КнопкаФормы"), Элементы.ГруппаРасчетСкидки);
		ЭлементКнопкаПересчитатьТаблицу.Заголовок 		= "Пересчитать таблицу";
		ЭлементКнопкаПересчитатьТаблицу.ИмяКоманды 		= "ПересчитатьТаблицу";
		ЭлементКнопкаПересчитатьТаблицу.Картинка 		= БиблиотекаКартинок.Обновить;
		ЭлементКнопкаПересчитатьТаблицу.Отображение 	= ОтображениеКнопки.КартинкаИТекст;
	КонецЕсли;
	//Конец.Ткачев_июнь 2025, homework-11-2 Задача №2.  Добавляем программно новый реквизит на форму	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура Д_СогласованнаяСкидкаПриИзменении()
	
	//Начало.Ткачев_июнь 2025, homework-11-2 Задача №2. Вызываем окно диалога для пересчета табличной части при изменении значения в реквизите "СогласованнаяСкидка"
		
	Если Объект.Товары.Количество() > 0 
		ИЛИ Объект.Услуги.Количество() > 0 Тогда
		ЗадатьВопросОПродолжении();  
	Иначе
		Возврат;
	КонецЕсли;    	
	//Конец.Ткачев_июнь 2025, homework-11-2 Задача №2. Вызываем окно диалога для пересчета табличной части при изменении значения в реквизите "СогласованнаяСкидка"
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗадатьВопросОПродолжении()
	
	//Начало.Ткачев_июнь 2025, homework-11-2 Задача №2. Алгоритм диалога для пересчета табличной части при изменении значения в реквизите "СогласованнаяСкидка"
	Перем Ответ, Режим;
	
	Режим = РежимДиалогаВопрос.ДаНет;
    Ответ = Ждать ВопросАсинх(НСтр("ru = 'Изменен процент скидки. Пересчитать таблицы товаров и услуг?';"), Режим, 0);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;  
	
	Д_ПересчитатьТабличнуюЧасть();	 
	//Конец.Ткачев_июнь 2025, homework-11-2 Задача №2. Алгоритм диалога для пересчета табличной части при изменении значения в реквизите "СогласованнаяСкидка"
			
КонецПроцедуры 

&НаКлиенте
Процедура Д_ПересчитатьТабличнуюЧасть()   
	
	//Начало.Ткачев_июнь 2025, homework-11-2 Задача №2.  Пересчитываем табличную часть для товаров и услуг с учетом скидки
	ТабличнаяЧасть = Объект.Товары;
	
	Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
		
		Д_ПересчитатьСтрокуТЧСУчетомСкидки(СтрокаТЧ);		
		
	КонецЦикла; 	
	
	ТабличнаяЧасть = Объект.Услуги;
	
	Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
		
		Д_ПересчитатьСтрокуТЧСУчетомСкидки(СтрокаТЧ);
		
	КонецЦикла;  	
	
	РассчитатьСуммуДокумента(); 
	
	//Конец.Ткачев_июнь 2025, homework-11-2 Задача №2.  Пересчитываем табличную часть для товаров и услуг с учетом скидки  
	
КонецПроцедуры  

&НаКлиенте
Процедура Д_ПересчитатьСтрокуТЧСУчетомСкидки(СтрокаТЧ)
	
	//Начало.Ткачев_июнь 2025, homework-11-2 Задача №2.  Отдельная процедура для расчета суммы по строке с учетом согласованной скидки 
	СтрокаТЧ.Сумма = СтрокаТЧ.Количество * (СтрокаТЧ.Цена - СтрокаТЧ.Цена * (Объект.Д_СогласованнаяСкидка / 100));
    //Конец.Ткачев_июнь 2025, homework-11-2 Задача №2.  Отдельная процедура для расчета суммы по строке с учетом согласованной скидки   
	
КонецПроцедуры 

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Д_ПересчитатьТаблицу(Команда)
	
	//Начало.Ткачев_июнь 2025, homework-11-2 Задача №2. Процедура для запуска команды по кнопке "ПересчитатьТаблицу"
	Д_ПересчитатьТабличнуюЧасть();     
	//Конец.Ткачев_июнь 2025, homework-11-2 Задача №2. Процедура для запуска команды по кнопке "ПересчитатьТаблицу"
		
КонецПроцедуры 

#КонецОбласти

#КонецОбласти
